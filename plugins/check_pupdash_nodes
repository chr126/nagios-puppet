#!/usr/bin/perl

# Copyright (c) 2012 Jason Hancock <jsnbyh@gmail.com>
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is furnished
# to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.


use strict;
use warnings;
use LWP;
use Nagios::Plugin;

my $np = Nagios::Plugin->new(
    usage     => "Usage: %s [-H|--host=<host> ] [ -p|--port=<port> ]",
    shortname => 'Dashboard Hosts',
);

$np->add_arg(
    spec => 'host|H=s',
    help => '-H, --host=Hostname or IP address'
);

$np->add_arg(
    spec    => 'port|p=s',
    help    => '-p, --port=port',
    default => 80,
);

$np->getopts;

my @interests = (
    'unresponsive',
    'failed',
    'pending',
    'changed',
    'unchanged',
    'unreported'
);

my $url = sprintf('http://%s:%d/', $np->opts->host, $np->opts->port);

my $ua = new LWP::UserAgent;
my $response = $ua->get($url);

if (!$response->is_success) {
    # We failed
    $np->nagios_exit('UNKNOWN', $response->code . ": " . $response->status_line);
}

# Parse the markup, loading up anything that matches into the %nodes hash
my %nodes;
foreach my $line(split('\n', $response->decoded_content)) {
    if($line=~m/<a href="\/nodes\/(\w+)">(\d+)<\/a>/) {
        $nodes{$1} = $2;
    }
}

foreach my $interest(@interests) {
   $np->add_perfdata(
        label => $interest,
        value => defined($nodes{$interest}) ? $nodes{$interest} : 0,
        uom   => undef,
    );
}

$np->nagios_exit('OK', '');
