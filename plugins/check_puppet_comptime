#!/usr/bin/perl

# Copyright (c) 2012 Jason Hancock <jsnbyh@gmail.com>
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is furnished
# to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.
#
# This file is part of the nagios-puppet bundle that can be found
# at https://github.com/jasonhancock/nagios-puppet
#
# This plugin reports the average puppet catalog compilation time based on the
# the logfile generated by the puppetmaster. By default, the messages are in
# the system log. You can control how many lines of the log are examined by
# adjusting the -n parameter. This script is designed to be run locally on 
# the puppetmaster and executed by NRPE

use strict;
use warnings;

use Nagios::Plugin;
use Nagios::Plugin::Threshold;

my $np = Nagios::Plugin->new(  
     usage => "Usage: %s [-c|--critical=<threshold> ] [ -w|--warning=<threshold> ] "
        . "[-f|--file=<path_to_file> ] [ -n|--numlines=<number_of_lines> ] ",
     shortname => 'Compile Time',
   );

$np->add_arg(
    spec => 'warning|w=s',
    help => '-w, --warning=Seconds'
);

$np->add_arg(
    spec => 'critical|c=s',
    help => '-c, --critical=Seconds'
);

$np->add_arg(
    spec    => 'file|f=s',
    help    => '-f, --file=Log file to parse. Defaults to /var/log/messages',
    default => '/var/log/messages'
);

$np->add_arg(
    spec    => 'numlines|n=s',
    help    => '-n, --numlines=Number of lines of logfile to examine. Defaults to 100.',
    default => 100
);

$np->getopts;

unless ( defined $np->opts->warning || defined $np->opts->critical ) {
    $np->nagios_die( " you didn't supply a threshold argument " );
}


my $cmd = sprintf('tail -n %d %s', $np->opts->numlines, $np->opts->file);
my @lines = `$cmd`;

my $sum=0;
my $count=0;

foreach my $line(@lines) {
    if($line=~m/Compiled catalog for .+ in environment .+ in (\d+\.\d+) seconds/) {
        $sum += $1;
        $count++;
    }
}

my $avg = sprintf('%.2f', $count > 0 ? $sum/$count : 0);

$np->add_perfdata( 
    label => 'catalog compile time',
    value => $avg,
    uom   => 'seconds',
    threshold => Nagios::Plugin::Threshold->set_thresholds(
        warning  => $np->opts->warning,
        critical => $np->opts->critical,
    )
);

##############################################################################
## check the result against the defined warning and critical thresholds,
## output the result and exit
$np->nagios_exit(
    return_code => $np->check_threshold($avg),
    message => " Average catalog compilation time: $avg seconds"
);

